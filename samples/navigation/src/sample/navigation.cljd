(ns sample.navigation
  "Port of https://docs.flutter.dev/cookbook/navigation/navigation-basics"
  (:require
   ["package:flutter/material.dart" :as m]
   [cljd.flutter :as f]))

(def second-route
  (m/Scaffold
    .appBar (m/AppBar .title (m/Text "Second Route"))
    .body
    (f/widget
      :get [m/Navigator]
      m/Center
      (m/ElevatedButton .onPressed #(.pop navigator))
      (m/Text "Go back!"))))

(def first-route
  (m/Scaffold
    .appBar (m/AppBar .title (m/Text "First Route"))
    .body
    (f/widget
      :get [m/Navigator]
      m/Center
      (m/ElevatedButton
        .onPressed #(do
                      (.push navigator (#/(m/MaterialPageRoute Object) .builder (f/build second-route)))
                      nil))
      (m/Text "Open route"))))

(defn main []
  (m/runApp
   (m/MaterialApp
    .title "Navigation Basics"
    .home first-route)))
