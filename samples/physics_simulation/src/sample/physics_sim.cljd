(ns sample.physics-sim
  "Faithful port of https://docs.flutter.dev/cookbook/animation/physics-simulation#interactive-example"
  (:require
   ["package:flutter/material.dart" :as m]
   ["package:flutter/physics.dart" :as p]
   [cljd.flutter.alpha2 :as f]))

;; cgrand's notes: still a relative straight port, however I chose to differ from
;; the Dart code for the handling of the _animation field.
;; Dart code uses a single listener which has a ref to a late mutable field.
;; The invariants between the value of this field, the controller and
;; the gesture detector was implicit.
;; I preferred to create a new listener at each PanEnd which closes over the animation.
(defn draggable-card [& {:keys [child]}]
  (f/widget
   :get [m/MediaQuery]
   :vsync vsync
   :managed [animator (m/AnimationController .vsync vsync)]
   :let [state (atom {:alignment m/Alignment.center})]
   :watch [{:keys [alignment animation]} state
           animated-alignment animation :refresh-on animation]
   :let [{:flds [width height]} (.-size media-query)
         run-animation
         (fn [^m/Offset {:flds [dx dy]}]
           (let [unit-velocity (.-distance (m/Offset (/ dx width) (/ dy height)))
                 spring (p/SpringDescription .mass 30 .stiffness 1 .damping 1)
                 simulation (p/SpringSimulation spring 0 1 (- unit-velocity))]
             (swap! state assoc :animation
               (.drive animator
                 (m/AlignmentTween
                   .begin alignment
                   .end m/Alignment.center)))
             (.animateWith animator simulation)))]
   (m/GestureDetector
     .onPanDown (fn [_]
                  (.stop animator)
                  (when animated-alignment
                    (swap! state assoc :animation nil :alignment animated-alignment))
                  nil)
     .onPanUpdate (fn [^m/DragUpdateDetails {{:flds [dx dy]} .-localPosition}]
                    (swap! state assoc :alignment
                      (m/Alignment
                        (- (* 2 (/ dx width)) 1)
                        (- (* 2 (/ dy height)) 1)))
                    nil)
     .onPanEnd (fn [^m/DragEndDetails details]
                 (run-animation (-> details .-velocity .-pixelsPerSecond))
                 nil))
   (m/Align .alignment (or animated-alignment alignment))
   m/Card
   child))

(def physics-card-demo
  (m/Scaffold
    .appBar (m/AppBar)
    .body (draggable-card :child (m/FlutterLogo .size 128))))

(defn main []
  (m/runApp (m/MaterialApp .home physics-card-demo)))
