(ns sample.drawer
  "Faithful port of https://docs.flutter.dev/cookbook/design/drawer"
  (:require ["package:flutter/material.dart" :as m]
            [cljd.flutter.alpha2 :as f]))

(defn my-home-page [title]
  (m/Scaffold
   .appBar (m/AppBar .title (m/Text title))
   .body (f/widget m/Center (m/Text "My Page!"))
   .drawer
   (f/widget
     m/Drawer
     ; Add a ListView to the drawer. This ensures the user can scroll
     ; through the options in the drawer if there isn't enough vertical
     ; space to fit everything.
     :get [m/Navigator]
     ; Important: Remove any padding from the ListView.
     (m/ListView .padding m/EdgeInsets.zero)
     .children
     (cons
       (f/widget
         (m/DrawerHeader .decoration (m/BoxDecoration .color m/Colors.blue))
         (m/Text "Drawer Header"))
       (for [title ["Item 1" "Item 2"]]
         (m/ListTile
           .title (m/Text title)
           .onTap
           ; Update the state of the app
           ; ...
           ; Then close the drawer
           #(.pop navigator)))))))

(defn main []
  (f/run m/MaterialApp .home (my-home-page "Drawer Demo")))
