(ns cljd.test-clojure.test-test
  (:use [cljd.test :only [deftest testing is use-fixtures]]))

(def ^:private test-state
  (atom {}))

(defn ^:private onetime-fixture
  ([] ;; setup
   (swap! test-state assoc :base-data :foo))
  ;; the "teardown" (1-arity) version is called with what the "setup" returned
  ([_] ;; teardown
   (reset! test-state {})))

(defn ^:private repeated-fixture
  "Resets test-state to what it was before running the tests"
  ([] ;;setup
   @test-state)
  ([start-state] ;; teardown
   (reset! test-state start-state)))

(use-fixtures :once onetime-fixture)
(use-fixtures :each repeated-fixture)

;; test order is not guaranteed, so both tests test both for mutation
;; and the reset of the state by the :each fixture

(deftest mutator-test-one
  (testing "Initial state"
    (is (or (= {:base-data :foo} @test-state)
            (= {:base-data :bar-2} @test-state))
        ":once fixtures are run before any tests and in the very end.
         if mutator-test-two ran first the value is expected to be :bar-2"))

  (testing "Mutating state"
    (swap! test-state assoc :base-data :bar-1)
    (is (= :bar-1 (:base-data @test-state)))

    (swap! test-state assoc :my-data :baz)
    (is (= :baz (:my-data @test-state))))

  (testing "Mutated state"
    (is (= :baz (:my-data @test-state))
        ":each fixtures are run around `deftest`, not individual `is` or `testing`")))

(deftest mutator-test-two
  (testing "Initial state"
    (is (or (= {:base-data :foo} @test-state)
            (= {:bar-data :bar-1} @test-state))
        ":once fixtures are run before any tests and in the very end.
         if mutator-test-one ran first the value is expected to be :bar-1"))

  (testing "Mutating state"
    (swap! test-state assoc :base-data :bar-2)
    (is (= :bar-2 (:base-data @test-state)))

    (swap! test-state assoc :my-data :baz)
    (is (= :baz (:my-data @test-state))))

  (testing "Mutated state"
    (is (= :baz (:my-data @test-state))
        ":each fixtures are run around `deftest`, not individual `is` or `testing`")))
