;   Copyright (c) Rich Hickey. All rights reserved.
;   The use and distribution terms for this software are covered by the
;   Eclipse Public License 1.0 (http://opensource.org/licenses/eclipse-1.0.php)
;   which can be found in the file epl-v10.html at the root of this distribution.
;   By using this software in any fashion, you are agreeing to be bound by
;   the terms of this license.
;   You must not remove this notice, or any other, from this software.

;;; test.clj: test framework for Clojure

;; by Stuart Sierra
;; March 28, 2009

;; Thanks to Chas Emerick, Allen Rohner, and Stuart Halloway for
;; contributions and suggestions.

(ns cljd.test
  "A unit testing framework.

   ASSERTIONS

   The core of the library is the \"is\" macro, which lets you make
   assertions of any arbitrary expression:

   (is (= 4 (+ 2 2)))
   (is (instance? Integer 256))
   (is (.startsWith \"abcde\" \"ab\"))

   The \"expected:\" line shows you the original expression, and the
   \"actual:\" shows you what actually happened.  In this case, it
   shows that (+ 2 2) returned 4, which is not = to 5.  Finally, the
   \"false\" on the last line is the value returned from the
   expression.  The \"is\" macro always returns the result of the
   inner expression.

   There are two special assertions for testing exceptions.  The
   \"(is (thrown? c ...))\" form tests if an exception of class c is
   thrown:

   (is (thrown? ArithmeticException (/ 1 0)))

   \"(is (thrown-with-msg? c re ...))\" does the same thing and also
   tests that the message on the exception matches the regular
   expression re:

   (is (thrown-with-msg? ArithmeticException #\"Divide by zero\"
                         (/ 1 0)))

   DOCUMENTING TESTS

   \"is\" takes an optional second argument, a string describing the
   assertion.  This message will be included in the error report.

   (is (= 5 (+ 2 2)) \"Crazy arithmetic\")

   In addition, you can document groups of assertions with the
   \"testing\" macro, which takes a string followed by any number of
   assertions.  The string will be included in failure reports.
   Calls to \"testing\" may be nested, and all of the strings will be
   joined together with spaces in the final report, in a style
   similar to RSpec <http://rspec.info/>

   (testing \"Arithmetic\"
     (testing \"with positive integers\"
       (is (= 4 (+ 2 2)))
       (is (= 7 (+ 3 4))))
     (testing \"with negative integers\"
       (is (= -4 (+ -2 -2)))
       (is (= -1 (+ 3 -4)))))

   Note that, unlike RSpec, the \"testing\" macro may only be used
   INSIDE a \"deftest\" or \"with-test\" form (see below).


   DEFINING TESTS

   There are two ways to define tests.  The \"with-test\" macro takes
   a defn or def form as its first argument, followed by any number
   of assertions.  The tests will be stored as metadata on the
   definition.

   (with-test
       (defn my-function [x y]
         (+ x y))
     (is (= 4 (my-function 2 2)))
     (is (= 7 (my-function 3 4))))

   As of Clojure SVN rev. 1221, this does not work with defmacro.
   See http://code.google.com/p/clojure/issues/detail?id=51

   The other way lets you define tests separately from the rest of
   your code, even in a different namespace:

   (deftest addition
     (is (= 4 (+ 2 2)))
     (is (= 7 (+ 3 4))))

   (deftest subtraction
     (is (= 1 (- 4 3)))
     (is (= 3 (- 7 4))))

   This creates functions named \"addition\" and \"subtraction\", which
   can be called like any other function.  Therefore, tests can be
   grouped and composed, in a style similar to the test framework in
   Peter Seibel's \"Practical Common Lisp\"
   <http://www.gigamonkeys.com/book/practical-building-a-unit-test-framework.html>

   (deftest arithmetic
     (addition)
     (subtraction))

   The names of the nested tests will be joined in a list, like
   \"(arithmetic addition)\", in failure reports.  You can use nested
   tests to set up a context shared by several tests.


   RUNNING TESTS

   At the commande line:

   dart test # run all tests
   dart test -N my.ns/ # run one test ns
   dart test -N my.ns/my.test # run one test

   FIXTURES

   Fixtures allow you to run code before and after tests, to set up
   the context in which tests should be run.

   A fixture is just a function of two arities.  It looks like this:

   (defn my-fixture
    ([]
      Perform setup, establish bindings, whatever.
      Return value will be passed as argument to the 1-arg arity.
      Thus you can pass references to resources to free etc.)
    ([resources]
     Tear-down / clean-up code here)

   Fixtures are attached to namespaces in one of two ways.  \"each\"
   fixtures are run repeatedly, once for each test function created
   with \"deftest\" or \"with-test\".  \"each\" fixtures are useful for
   establishing a consistent before/after state for each test, like
   clearing out database tables.

   \"each\" fixtures can be attached to the current namespace like this:
   (use-fixtures :each fixture1 fixture2 ...)
   The fixture1, fixture2 are just functions like the example above.
   They can also be anonymous functions, like this:
   (use-fixtures :each (fn [f] setup... (f) cleanup...))

   The other kind of fixture, a \"once\" fixture, is only run once,
   around ALL the tests in the namespace.  \"once\" fixtures are useful
   for tasks that only need to be performed once, like establishing
   database connections, or for time-consuming tasks.

   Attach \"once\" fixtures to the current namespace like this:
   (use-fixtures :once fixture1 fixture2 ...)

   EXTENDING TEST-IS (ADVANCED & NOT YET SUPPORTED)

   You can extend the behavior of the \"is\" macro by defining new
   methods for the \"assert-expr\" multimethod.  These methods are
   called during expansion of the \"is\" macro, so they should return
   quoted forms to be evaluated.

   You can plug in your own test-reporting framework by rebinding
   the \"report\" function: (report event)

   The 'event' argument is a map.  It will always have a :type key,
   whose value will be a keyword signaling the type of event being
   reported.  Standard events with :type value of :pass, :fail, and
   :error are called when an assertion passes, fails, and throws an
   exception, respectively.  In that case, the event will also have
   the following keys:

     :expected   The form that was expected to be true
     :actual     A form representing what actually occurred
     :message    The string message given as an argument to 'is'

   The \"testing\" strings will be a list in \"*testing-contexts*\", and
   the vars being tested will be a list in \"*testing-vars*\".

   Your \"report\" function should wrap any printing calls in the
   \"with-test-out\" macro, which rebinds *out* to the current value
   of *test-out*.

   For additional event types, see the examples in the code."
  {:author "Stuart Sierra, with contributions and suggestions by
  Chas Emerick, Allen Rohner, and Stuart Halloway"}
  (:require [cljd.template :as temp]
            [cljd.string :as str]
            ["package:test/test.dart" :as dt]))

;;; UTILITIES FOR ASSERTIONS

(defn ^:macro-support assert-predicate
  "Returns generic assertion code for any functional predicate.  The
  'expected' argument to 'report' will contains the original form, the
  'actual' argument will contain the form with all its sub-forms
  evaluated.  If the predicate returns false, the 'actual' form will
  be wrapped in (not...)."
  [msg form]
  (let [args (rest form)
        pred (first form)]
    `(let [values# (list ~@args)
           result# (apply ~pred values#)]
       (when-not result#
         {:type :fail, :message ~msg,
          :actual (list '~'not (cons '~pred values#))}))))

(defn ^:macro-support assert-any
  "Returns generic assertion code for any test, including macros, Java
  method calls, or isolated symbols."
  [msg form]
  `(let [value# ~form]
     (when-not value#
       {:type :fail, :message ~msg, :actual value#})))

;;; ASSERTION METHODS

;; You don't call these, but you can add methods to extend the 'is'
;; macro.  These define different kinds of tests, based on the first
;; symbol in the test expression.


(defn- ^:macro-support resolve-symbol [env sym] ; TODO migrate to core when design settles
  (let [{:keys [libs] :as nses} (:nses env)
        {:keys [mappings aliases] :as current-ns} (nses (:current-ns nses))]
    (when-not (env sym)
      (or (current-ns sym)
        (if-some [v (mappings sym)]
          (recur env v)
          (let [sym-ns (namespace sym)
                lib-ns (if (= "clojure.core" sym-ns)
                         "cljd.core"
                         (some-> (get aliases sym-ns) libs :ns name))]
            (if (some-> lib-ns (not= sym-ns))
              (recur env (symbol lib-ns (name sym)))
              (some-> sym-ns symbol nses (get (symbol (name sym)))))))))))

(defn- ^:macro-support function? [env sym]
  (and (symbol? sym) (when-some [v (resolve-symbol env sym)] (not (:macro (:meta v))))))

(defn ^:macro-support assert-expr [env msg form]
  (case (cond
          (nil? form) :always-fail
          (seq? form) (first form)
          :else :default)
    :always-fail
    ;; nil test: always fail
    `{:type :fail, :message ~msg}
    thrown?
    ;; (is (thrown? c expr))
    ;; Asserts that evaluating expr throws an exception of class c.
    ;; Returns the exception thrown.
    (let [klass (second form)
          body (nthnext form 2)]
      `(try
         {:type :fail, :message ~msg,
          :actual (do ~@body)}
         (catch ~klass e# nil)))
    instance? ; TODO dart/is? or satisfies?
    ;; Test if x is an instance of y.
    (let [klass (nth form 1)]
      `(let [object# ~(nth form 2)]
         (when-not (dart/is? object# ~klass)
           {:type :fail, :message ~msg,
            :expected '~form, :actual (.-runtimeType object#)})))
    thrown-with-msg?
    ;; (is (thrown-with-msg? c re expr))
    ;; Asserts that evaluating expr throws an exception of class c.
    ;; Also asserts that the message string of the exception matches
    ;; (with re-find) the regular expression re.
    (let [klass (nth form 1)
          re (nth form 2)
          body (nthnext form 3)]
      `(try
         {:type :fail, :message ~msg, :expected '~form,
          :actual (do ~@body)}
         (catch ~klass e#
           (when-not (re-find ~re (.-message e#))
             {:type :fail, :message ~msg,
              :expected '~form, :actual e#}))))
    (if (and (sequential? form) (function? env (first form)))
      (assert-predicate msg form)
      (assert-any msg form))))

(deftype Result [m s]
  Object
  (toString [self] s))

(defn result [m]
  (Result. m
    (if-some [e (:exception m)]
      (str "has thrown " e)
      (pr-str (:actual m)))))

(defn cljd-test-matcher [form]
  (reify
    :extends dt/Matcher
    (^dt/Description describe [_ ^dt/Description description]
     (doto description (.add (pr-str form))))
    (^bool matches [_ r ^Map match-state]
     (if-some [m (.-m ^Result r)]
       (do
         (dt/addStateInfo match-state m)
         false)
       true))
    (^dt/Description describeMismatch [_ r ^dt/Description mismatch-description ^Map match-state ^bool verbose]
     (doto mismatch-description
       (cond->
           (:type match-state)
         (.add
           (with-out-str
             (when-let [message (:message match-state)] (println message))
             (case (:type match-state)
               :fail nil
               :error
               (do
                 (prn (:exception match-state))
                 (prn (:stacktrace match-state))))
             (when-some [[group & more-groups] (seq *groups*)]
               (print "occured in: ")
               (when more-groups (newline))
               (println group)
               (doseq [g more-groups] (println g))))))))))

;;; ASSERTION MACROS

;; You use these in your tests.

(def ^:dynamic *groups* ())
(def ^:dynamic *in-test* false)

(defmacro is
  "Generic assertion macro.  'form' is any predicate test.
  'msg' is an optional message to attach to the assertion.

  Example: (is (= 4 (+ 2 2)) \"Two plus two should be 4\")

  Special forms:

  (is (thrown? c body)) checks that an instance of c is thrown from
  body, fails if not; then returns the thing thrown.

  (is (thrown-with-msg? c re body)) checks that an instance of c is
  thrown AND that the message on the exception matches (with
  re-find) the regular expression re."
  ([form] `(is ~form nil))
  ([form msg]
   `(let [r# (result
               (try
                 ~(assert-expr &env msg form)
                 (catch Object t# st#
                        {:type :error
                         :exception t#
                         :stacktrace st#})))]
      (dt/expect r# (cljd-test-matcher '~form)))))

(defmacro are
  "Checks multiple assertions with a template expression.
  See clojure.template/do-template for an explanation of
  templates.

  Example: (are [x y] (= x y)
                2 (+ 1 1)
                4 (* 2 2))
  Expands to:
           (do (is (= 2 (+ 1 1)))
               (is (= 4 (* 2 2))))

  Note: This breaks some reporting features, such as line numbers."
  {:added "1.1"}
  [argv expr & args]
  (if (or
       ;; (are [] true) is meaningless but ok
       (and (empty? argv) (empty? args))
       ;; Catch wrong number of args
       (and (pos? (count argv))
            (pos? (count args))
            (zero? (mod (count args) (count argv)))))
    `(temp/do-template ~argv (is ~expr) ~@args)
    (throw (argument-error "The number of args doesn't match are's argv."))))

(defmacro testing
  "Adds a new string to the list of testing contexts.  May be nested,
  but must occur inside a test function (deftest)."
  [string & body]
  `(binding [*groups* (conj *groups* ~string)] ~@body))

;;; DEFINING TESTS

(defn fixture-callbacks [f]
  (let [v (volatile! nil)]
    [#(vreset! v (f)) #(f @v)]))

(defn register-once-fixtures [fixtures-callbacks]
  (doseq [[setUp tearDown] fixtures-callbacks]
    (dt/setUpAll setUp)
    (dt/tearDownAll tearDown)))

(defn register-each-fixtures [fixtures-callbacks]
  (doseq [[setUp tearDown] fixtures-callbacks]
    (dt/setUp setUp)
    (dt/tearDown tearDown)))

(defn register-fixtures [fixtures-callbacks]
  (register-once-fixtures fixtures-callbacks))

(defmacro use-fixtures [type & fns]
  `(do
     (def ~(case type
             :once 'cljd-test-once-fixtures
             :each 'cljd-test-each-fixtures
             (throw (argument-error "First argument to cljs.test/use-fixtures must be :once or :each")))
       (map fixture-callbacks [~@fns]))
     (defrunner-main)))

(defmacro defrunner-main []
  (let [{:keys [current-ns] :as nses} (:nses &env)
        the-ns (nses current-ns)
        tests (keep (fn [[k v]] (when (and (symbol? k) (::test (:meta v))) (list k))) the-ns)]
    `(defn ~'^:dart/test main []
       ~(when (the-ns 'cljd-test-once-fixtures)
          `(register-once-fixtures ~'cljd-test-once-fixtures))
       ~@tests)))

(defmacro deftest
  "Defines a test function with no arguments.  Test functions may call
  other tests, so tests may be composed.  If you compose tests, you
  should also define a function named test-ns-hook; run-tests will
  call test-ns-hook instead of testing all vars.

  Note: Actually, the test body goes in the :test metadata on the var,
  and the real function (the value of the var) calls test-var on
  itself.

  When *load-tests* is false, deftest is ignored."
  [name & body]
  (let [{:keys [current-ns] :as nses} (:nses &env)
        the-ns (nses current-ns)]
    `(do
       (defn ~(vary-meta name assoc ::test true) []
         (let [name# ~(str (:current-ns (:nses &env)) "/" name)
               thunk#
               (fn []
                 ~(when (the-ns 'cljd-test-each-fixtures)
                    `(register-each-fixtures ~'cljd-test-each-fixtures))
                 (testing name# ~@body))]
           (if *in-test*
             (thunk#)
             (binding [*in-test* true]
               (dt/test name# thunk#)))))
       (defrunner-main))))

(defmacro deftest-
  "Like deftest but creates a private var."
  [name & body]
  `(deftest ~(vary-meta name assoc :private true) ~@body))
